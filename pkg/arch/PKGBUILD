# Maintainer: Otreblan <alberto.oporto@utec.edu.pe>

# shellcheck shell=bash disable=SC2034,SC2154,SC2164

pkgname=mini-crypto-git
pkgver=r41.66044ee
pkgrel=1
epoch=
pkgdesc="Toy blockchain"
arch=('x86_64')
url="https://github.com/utec-2023-2-seguridad/mini-crypto"
license=('GPL3')
groups=()
depends=('gcc-libs' 'libsodium' 'boost-libs')
makedepends=(
	'bash-completion'
	'boost'
	'cmake'
	'git'
	'rapidjson'
)
checkdepends=()
optdepends=()
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
_gitrepo="$(git rev-parse --show-toplevel)"
_headcommit="$(git rev-parse HEAD)"
source=("$pkgname::git+file://$_gitrepo#commit=$_headcommit")
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/$pkgname"
	( set -o pipefail
	git describe --long 2>/dev/null | sed 's/^v-//;s/\([^-]*-g\)/r\1/;s/-/./g' ||
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
	)
}

build() {
	cmake -B build -S "$pkgname" \
		-DCMAKE_BUILD_TYPE='None' \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INTERPROCEDURAL_OPTIMIZATION=ON \
		-DCMAKE_EXPORT_COMPILE_COMMANDS=ON \
		-DCMAKE_UNITY_BUILD=ON

	cmake --build build
}

package() {
	DESTDIR="$pkgdir" cmake --install build

	# https://stackoverflow.com/a/77320297
	rm -r "$pkgdir"/usr/include
	rm -r "$pkgdir"/usr/lib
}
